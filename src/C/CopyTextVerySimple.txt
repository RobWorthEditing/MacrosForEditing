
Sub CopyTextVerySimple()
' Paul Beverley - Version 19.07.25
' Creates a text-only copy, with no features preserved

myDelay = 1

Set rngOld = ActiveDocument.Content
myLanguage = rngOld.Characters(1).LanguageID

Set oldDoc = ActiveDocument
Set newDoc = Documents.Add
Set rng = newDoc.Content
rng.Text = rngOld.Text
myCount = myDelay * 100
For i = 1 To myCount: DoEvents: Next i

rng.LanguageID = myLanguage
numNotes = oldDoc.Endnotes.Count
If numNotes > 0 Then
  Set rng = ActiveDocument.Content
  rng.Collapse wdCollapseEnd
  rng.InsertAfter Text:=vbCr & "Endnotes:" & vbCr & vbCr
  rng.Collapse wdCollapseEnd
  rng.InsertAfter Text:= _
       oldDoc.StoryRanges(wdEndnotesStory).FormattedText
End If

numNotes = oldDoc.Footnotes.Count
If numNotes > 0 Then
  Set rng = ActiveDocument.Content
  rng.Collapse wdCollapseEnd
  rng.InsertAfter Text:=vbCr & "Footnotes:" & vbCr & vbCr
  rng.Collapse wdCollapseEnd
  rng.InsertAfter Text:= _
       oldDoc.StoryRanges(wdFootnotesStory).FormattedText
End If

' copy all the textboxes to the end of the text
shCount = oldDoc.Shapes.Count
If shCount > 0 Then
  Selection.EndKey Unit:=wdStory
  Selection.TypeText vbCr & "Textboxes:" & vbCr & vbCr
  For j = 1 To shCount
    Set shp = oldDoc.Shapes(j)
    If shp.Type <> 24 And shp.Type <> 3 Then
      If shp.TextFrame.HasText Then
        Set rng = shp.TextFrame.TextRange
        If Len(rng.Text) > 1 Then
          Selection.Text = rng.Text
          Selection.EndKey Unit:=wdStory
        End If
      End If
    End If
    DoEvents
  Next
End If

Set rng = ActiveDocument.Content
With rng.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "^2"
  .Wrap = wdFindContinue
  .Replacement.Text = ""
  .MatchWildcards = False
  .Execute Replace:=wdReplaceAll
  DoEvents
End With

' Add a newline for safety
newDoc.Content.InsertAfter Text:=vbCr

Selection.HomeKey Unit:=wdStory
Beep
End Sub

