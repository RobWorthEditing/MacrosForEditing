Sub MultiFileText()
' Paul Beverley - Version 05.01.22
' Collects text plus simple formatting from multiple files

spaceEquations = True
listOff = True
acceptTCs = True
addFilename = True
convertMathsItems = True
remindAboutCancel = False

myScreenOff = True

CR2 = vbCr & vbCr
Dim allMyFiles(200) As String
Set rng = ActiveDocument.Content
myExtent = 250

' First find if this is a Mac or a PC!
myFullName = ActiveDocument.FullName
myName = ActiveDocument.Name
myFolder = Replace(myFullName, myName, "")
myDelimiter = Application.PathSeparator
If myDelimiter = "/" Then
  InAMac = True
Else
  InAMac = False
End If

If rng.End - rng.Start > myExtent Then rng.End = rng.Start + myExtent
If InStr(LCase(rng.Text), ".doc") = 0 _
     And InStr(LCase(rng.Text), ".rtf") = 0 _
     And InStr(LCase(rng.Text), ".pdf") = 0 Then
' If not a file list then navigate to the required folder
  If remindAboutCancel = True Then myResponse = _
       MsgBox("Navigate to the required folder; then press 'Cancel'" _
      , , "Multifile Text Collection")
  docCount = Documents.count
  Dialogs(wdDialogFileOpen).Show
  If Documents.count > docCount Then ActiveDocument.Close
  dirPath = CurDir()
  ChDir dirPath
' Read the names of all the files in this directory
  myFile = Dir(CurDir() & myDelimiter)
  Documents.Add
  numFiles = 0
  Do While myFile <> ""
    If InStr(LCase(myFile), ".doc") > 0 Or _
         InStr(LCase(myFile), ".rtf") > 0 Or _
         InStr(LCase(myFile), ".pdf") > 0 Then
      Selection.TypeText myFile & vbCr
      numFiles = numFiles + 1
    End If
    myFile = Dir()
  Loop

' Now sort the file list (only actually needed for Macs)
  Selection.WholeStory
  Selection.Sort SortOrder:=wdSortOrderAscending, _
       SortFieldType:=wdSortFieldAlphanumeric
  Selection.EndKey Unit:=wdStory
  Selection.TypeParagraph
  Selection.HomeKey Unit:=wdStory
  Selection.TypeText dirPath
' Go back until you hit myDelimiter
  Selection.MoveStartUntil cset:=myDelimiter, count:=wdBackward
  dirName = Selection
  Selection.HomeKey Unit:=wdStory

  myResponse = MsgBox("Collect unformatted text from ALL the files in" & _
       " directory: " & dirName & "?", vbQuestion + vbYesNoCancel, _
       "Multifile Text Collection")
  If myResponse <> vbYes Then Exit Sub
Else
  myResponse = MsgBox("Collect unformatted text from the files listed here?", _
       vbQuestion + vbYesNoCancel, "Multifile Text Collection")
  If myResponse <> vbYes Then Exit Sub
End If
If InAMac = False Then
  ActiveDocument.ActiveWindow.WindowState = wdWindowStateNormal
  Application.Move Left:=20, Top:=300
  Application.Resize Width:=1000, Height:=200
End If

' Pick up the folder name and the filenames from the file list
numFiles = 0

If myScreenOff = True Then
  Application.ScreenUpdating = False
  On Error GoTo ReportIt
End If

myFolder = ""
For Each myPara In ActiveDocument.Paragraphs
  myPara.Range.Select
  Selection.MoveEnd , -1
  lineText = Selection
  If myFolder = "" Then
    myFolder = lineText
    Selection.Collapse wdCollapseEnd
    Selection.MoveStartUntil cset:=myDelimiter, count:=wdBackward
    Selection.MoveStart , -1
    myDelimiter = Left(Selection, 1)
  Else
    thisFile = lineText
    If Len(thisFile) > 2 Then
      If Left(thisFile, 1) <> "|" Then
        numFiles = numFiles + 1
        allMyFiles(numFiles) = thisFile
      End If
    End If
  End If
Next myPara
Selection.HomeKey Unit:=wdStory

Set alltextDoc = Documents.Add
Set alltextRng = ActiveDocument.Content
Selection.HomeKey Unit:=wdStory
gotLanguage = False

For i = 1 To numFiles
' Get the folder name, and then the text for the files in the list
  StatusBar = allMyFiles(i)
  thisFile = myFolder & myDelimiter & allMyFiles(i)
  Set myDoc = Application.Documents.Open(FileName:=thisFile)

  If gotLanguage = False Then
    Selection.MoveEnd , 1
    myLanguage = Selection.Range.LanguageID
    gotLanguage = True
  End If
  numEqns = ActiveDocument.OMaths.count
  If numEqns > 0 And spaceEquations = True Then
    For Each myMath In ActiveDocument.OMaths
      myMath.Range.InsertBefore Text:=" "
      DoEvents
      numEqns = numEqns - 1
    Next myMath
  End If
  DoEvents
  If acceptTCs = True Then myDoc.Revisions.AcceptAll
  myDoc.TrackRevisions = False
  If listOff = True Then myDoc.ConvertNumbersToText
  If ActiveDocument.Endnotes.count > 0 Then
    Set thisDocRange = myDoc.Content
    thisDocRange.Collapse wdCollapseEnd
    thisDocRange.FormattedText = _
         myDoc.StoryRanges(wdEndnotesStory).FormattedText
  End If
  If ActiveDocument.Footnotes.count > 0 Then
    Set thisDocRange = myDoc.Content
    thisDocRange.Collapse wdCollapseEnd
    thisDocRange.FormattedText = _
         myDoc.StoryRanges(wdFootnotesStory).FormattedText
  End If

  ' copy all the textboxes to the end of the text
  shCount = myDoc.Shapes.count
  If shCount > 0 Then
    Selection.EndKey Unit:=wdStory
    Selection.TypeText vbCr & vbCr
    For j = 1 To shCount
      Set shp = ActiveDocument.Shapes(j)
      If shp.Type <> 24 And shp.Type <> 3 Then
        If shp.TextFrame.HasText Then
          Set rng = shp.TextFrame.TextRange
          Selection.FormattedText = rng.FormattedText
          Selection.EndKey Unit:=wdStory
        End If
      End If
    Next
  End If

  StatusBar = allMyFiles(i)
  Selection.HomeKey Unit:=wdStory
  If addFilename = True Then Selection.TypeText CR2 & "[[[[ " & _
       allMyFiles(i) & " ]]]]]" & CR2
  If convertMathsItems = True Then
    numMaths = ActiveDocument.OMaths.count
    If numMaths > 0 Then
      For Each myMath In ActiveDocument.OMaths
        myMath.Range.Select
        eqText = Replace(Selection.Text, vbCr, " ")
        Selection.MoveStart , -1
        Selection.Delete
        startHere = Selection.Start
        Selection.TypeText eqText
        Selection.Start = startHere
      Next myMath
    End If
  End If
  
  myDoc.Fields.Unlink
  ' Accept all track changes
  myDoc.Range.Revisions.AcceptAll
  myDoc.TrackRevisions = False
  Set rng = ActiveDocument.Content
  With rng.Find
    .ClearFormatting
    .Replacement.ClearFormatting
    .Text = ""
    .Font.Italic = True
    .Wrap = wdFindContinue
    .MatchWildcards = False
    .MatchCase = True
    .Replacement.Text = "zccz^&pqqp"
    .Execute Replace:=wdReplaceAll
    
    .ClearFormatting
    .Replacement.ClearFormatting
    .Text = ""
    .Font.Bold = True
    .Replacement.Text = "bqqb^&zwvf"
    .Execute Replace:=wdReplaceAll
    
    .ClearFormatting
    .Replacement.ClearFormatting
    .Text = ""
    .Font.Superscript = True
    .Replacement.Text = "yxzu^&qiwv"
    .Execute Replace:=wdReplaceAll
    
    .ClearFormatting
    .Replacement.ClearFormatting
    .Text = ""
    .Font.Subscript = True
    .Replacement.Text = "xhwc^&yvxz"
    .Execute Replace:=wdReplaceAll
  End With

  Set rng = ActiveDocument.Content
  alltextRng.End = alltextDoc.Range.End
  alltextRng.Start = alltextDoc.Range.End
  alltextRng.Text = rng.Text
  myDoc.Close SaveChanges:=wdDoNotSaveChanges
Next i

Set rng = alltextDoc.Content
rng.LanguageID = myLanguage
rng.NoProofing = False

Set rng = ActiveDocument.Content
With rng.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "yxzu(*)qiwv"
  .MatchWildcards = True
  .MatchCase = False
  .Replacement.Text = "^&"
  .Replacement.Font.Superscript = True
  .Execute Replace:=wdReplaceAll
  
    .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "xhwc(*)yvxz"
  .Replacement.Text = "^&"
  .Replacement.Font.Subscript = True
  .Execute Replace:=wdReplaceAll
  
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "bqqb(*)zwvf"
  .Replacement.Text = "^&"
  .Replacement.Font.Bold = True
  .Execute Replace:=wdReplaceAll
  
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "zccz(*)pqqp"
  .Replacement.Text = "^&"
  .Replacement.Font.Italic = True
  .Execute Replace:=wdReplaceAll
  
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "zccz"
  .MatchWildcards = False
  .MatchCase = False
  .Replacement.Text = ""
  .Execute Replace:=wdReplaceAll
  
  .Text = "yxzu"
  .Replacement.Text = ""
  .Execute Replace:=wdReplaceAll
  
  .Text = "bqqb"
  .Replacement.Text = ""
  .Execute Replace:=wdReplaceAll
  
  .Text = "zwvf"
  .Replacement.Text = ""
  .Execute Replace:=wdReplaceAll
  
  .Text = "qiwv"
  .Replacement.Text = ""
  .Execute Replace:=wdReplaceAll
  
  .Text = "pqqp"
  .Replacement.Text = ""
  .Execute Replace:=wdReplaceAll
  
  .Text = "xhwc"
  .Replacement.Text = ""
  .Execute Replace:=wdReplaceAll
  
  .Text = "yvxz"
  .Replacement.Text = ""
  .Execute Replace:=wdReplaceAll

' Remove optional hyphens
  .Text = Chr(172)
  .MatchWildcards = False
  .MatchCase = True
  .Replacement.Text = ""
  .Execute Replace:=wdReplaceAll
End With

Set rng = ActiveDocument.Content
totChars = rng.End
' Add one '[' for each file, to check they are all there
With rng.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "[[[[ "
  .Wrap = wdFindContinue
  .MatchWildcards = False
  .MatchWholeWord = False
  .Replacement.Text = "[[[[[ "
  .Execute Replace:=wdReplaceAll
End With
ActiveDocument.Paragraphs(3).Range.Bold = True
Set rng = ActiveDocument.Content
totCharsNow = rng.End
If InAPC = True Then
  ActiveDocument.ActiveWindow.WindowState = wdWindowStateNormal
  Application.Resize Width:=1000, Height:=500
End If
Beep
With Selection.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "[[[[["
  .Wrap = wdFindContinue
  .MatchWildcards = False
  .MatchWholeWord = False
  .Replacement.Text = ""
  .Execute
End With
Selection.Collapse wdCollapseEnd

Application.ScreenUpdating = True

If totCharsNow - totChars <> numFiles Then
  Beep
  myTime = Timer
  Do
  Loop Until Timer > myTime + 0.2
  Beep
  MsgBox "Warning: All files might not have been included."
End If
Exit Sub

ReportIt:
Application.Visible = True
On Error GoTo 0
Resume
End Sub

