Sub PunctuationToSingleQuote()
' Paul Beverley - Version 03.05.17
' Changes next quote mark to single curly

trackIt = False

myTrack = ActiveDocument.TrackRevisions
If trackIt = False Then ActiveDocument.TrackRevisions = False

searchChars = Chr(34) & Chr(39) & ChrW(8216) & ChrW(8217) _
       & ChrW(8220) & ChrW(8221) & ChrW(8249) & ChrW(8250) _
       & ChrW(8222) & ChrW(8218) & ChrW(171) & ChrW(187) & ChrW(96) _
       & ChrW(8242) & ChrW(8243)

Selection.Collapse wdCollapseEnd
startHere = Selection.Start
Set rng = ActiveDocument.Content
theEnd = rng.End
Do While InStr(searchChars, Selection) = 0
  Selection.MoveRight , 1
  If Selection.Start > startHere + 1000 Or _
       Selection.Start > theEnd - 2 Then
    Beep
    ActiveDocument.TrackRevisions = myTrack
    Exit Sub
  End If
Loop
Selection.MoveEnd wdCharacter, 1
thisChar = Selection
Selection.Collapse wdCollapseStart

optNow = Options.AutoFormatAsYouTypeReplaceQuotes
Options.AutoFormatAsYouTypeReplaceQuotes = True

With Selection.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = thisChar
  .Replacement.Text = "'"
  .Execute Replace:=wdReplaceOne
End With
Selection.Collapse wdCollapseEnd

Options.AutoFormatAsYouTypeReplaceQuotes = optNow
ActiveDocument.TrackRevisions = myTrack
End Sub

