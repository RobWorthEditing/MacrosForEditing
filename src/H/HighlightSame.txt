
Sub HighlightSame()
' Paul Beverley - Version 27.03.25
' Highlights all occurrences of this text in this colour

textColour = wdYellow

' doMatchCase = True
doMatchCase = False

nonTextColour = wdGray25

' Preserve TC status and existing highlight colour
oldColour = Options.DefaultHighlightColorIndex
nowTrack = ActiveDocument.TrackRevisions
ActiveDocument.TrackRevisions = False

Dim v As Variable, nowColour As Long
varsExist = False
For Each v In ActiveDocument.Variables
  If v.Name = "selStart" Then varsExist = True: Exit For
Next v

If varsExist Then
  wasStart = ActiveDocument.Variables("selStart")
  wasEnd = ActiveDocument.Variables("selEnd")
  If Selection.start > wasStart - 1 And Selection.End < _
       wasEnd + 1 And wasEnd - wasStart < 200 Then
    Selection.start = wasStart
    Selection.End = wasEnd
  End If
End If

Set rng = Selection.Range.Duplicate
nonText = (rng.Text = " ")
wasSelected = (rng.End > rng.start + 1)
If Not (wasSelected) Then
  If nonText = True Then
    nowColour = Selection.Range.HighlightColorIndex
    If nowColour = wdNoHighlight Then
      Options.DefaultHighlightColorIndex = nonTextColour
    Else
      Options.DefaultHighlightColorIndex = wdNoHighlight
    End If
  Else
    If UCase(rng) <> LCase(rng) And rng.HighlightColorIndex > 0 Then
      Options.DefaultHighlightColorIndex = wdNoHighlight
    Else
      nowColour = Selection.Range.HighlightColorIndex
      If nowColour = wdNoHighlight Then
        Options.DefaultHighlightColorIndex = textColour
      Else
        Options.DefaultHighlightColorIndex = nowColour
      End If
    End If
    rng.Expand wdWord
    Do While InStr(ChrW(8217) & "' ", Right(rng.Text, 1)) > 0
      rng.MoveEnd , -1
      DoEvents
    Loop
  End If
Else
  partWord = True
  nowColour = Selection.Range.HighlightColorIndex
'  If nowColour = 0 Then nowColour = nontextColour
  If nowColour > 1000 Then
    Set rng = ActiveDocument.Content
    rng.start = Selection.start
    rng.End = Selection.start + 1
    nowColour = rng.HighlightColorIndex
  Else
    nowColour = textColour
  End If
  Options.DefaultHighlightColorIndex = nowColour
End If

findText = rng.Text
Select Case Asc(findText)
  Case 9:  findText = "^t"
  Case 30:  findText = "^~": partWord = True ' non-breaking hyphen
End Select

Selection.Collapse wdCollapseStart
With Selection.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = Trim(findText)
  .MatchCase = doMatchCase
  .Forward = True
  .Replacement.Text = "^&"
  .Replacement.Highlight = True
  .Wrap = wdFindContinue
  Debug.Print partWord, nonText
  If textSelected = True And nonText = False Then .MatchWholeWord = True
  .MatchWildcards = False
  .Execute Replace:=wdReplaceAll
End With

' Restore to original state
Options.DefaultHighlightColorIndex = oldColour
ActiveDocument.TrackRevisions = nowTrack
End Sub

