
Sub HighlightSame()
' Paul Beverley - Version 19.07.25
' Highlights all occurrences of this text in this colour

' defaultTextColour = wdBrightGreen
defaultTextColour = wdYellow

' doMatchCase = True
doMatchCase = False

' Preserve TC status and existing highlight colour
oldColour = Options.DefaultHighlightColorIndex
nowTrack = ActiveDocument.TrackRevisions
ActiveDocument.TrackRevisions = False

Dim v As Variable, nowColour As Long
varsExist = False
For Each v In ActiveDocument.Variables
  If v.Name = "selStart" Then varsExist = True: Exit For
Next v

If varsExist Then
  wasStart = ActiveDocument.Variables("selStart")
  wasEnd = ActiveDocument.Variables("selEnd")
  If Selection.Start > wasStart - 1 And Selection.End < _
       wasEnd + 1 And wasEnd - wasStart < 200 Then
    Selection.Start = wasStart
    Selection.End = wasEnd
  End If
End If

Set rng = Selection.Range.Duplicate
nowColour = rng.Characters(1).HighlightColorIndex
wasSelected = (rng.Start >= wasStart) And (rng.End <= wasEnd)
Debug.Print "wasSelected = "; wasSelected
If wasSelected Then
  Options.DefaultHighlightColorIndex = nowColour
Else
  If nowColour = 0 Then
    Options.DefaultHighlightColorIndex = defaultTextColour
  Else
    Options.DefaultHighlightColorIndex = nowColour
  End If
End If
If rng.Start = rng.End Then
  rng.Expand wdWord
  Do While InStr(ChrW(8217) & "' ", Right(rng.Text, 1)) > 0
    rng.MoveEnd , -1
    DoEvents
  Loop
End If
findText = rng.Text

myDo = "TEF"
If ActiveDocument.Footnotes.Count = 0 Then myDo = Replace(myDo, "F", "")
If ActiveDocument.Endnotes.Count = 0 Then myDo = Replace(myDo, "E", "")

For myRun = 1 To Len(myDo)
  doIt = Mid(myDo, myRun, 1)
  Select Case doIt
     Case "T": Set rng = ActiveDocument.Content
     Case "F": Set rng = ActiveDocument.StoryRanges(wdFootnotesStory)
     Case "E": Set rng = ActiveDocument.StoryRanges(wdEndnotesStory)
  End Select

  With rng.Find
    .ClearFormatting
    .Replacement.ClearFormatting
    .Text = Trim(findText)
    .MatchCase = doMatchCase
    .Forward = True
    .Replacement.Text = "^&"
    .Replacement.Highlight = True
    .Wrap = wdFindContinue
    Debug.Print partWord, nonText
    If textSelected = True And nonText = False Then .MatchWholeWord = True
    .MatchWildcards = False
    .Execute Replace:=wdReplaceAll
  End With
Next myRun

' Restore to original state
Options.DefaultHighlightColorIndex = oldColour
ActiveDocument.TrackRevisions = nowTrack
End Sub

