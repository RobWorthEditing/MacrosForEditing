Sub PunctuationItalicOff()
' Paul Beverley - Version 08.06.17
' Un-italicises all commas not followed by italic text

' stayItalicColour = wdYellow
stayItalicColour = wdNoHighlight

changeToRomanColour = wdGray25
trackit = False

Set rng = ActiveDocument.Content
With rng.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "[,:;.]"
  .Font.Italic = True
  .Wrap = wdFindStop
  .Replacement.Text = ""
  .Forward = True
  .MatchWildcards = True
  .Execute
End With

myTrack = ActiveDocument.TrackRevisions
If trackit = False Then ActiveDocument.TrackRevisions = False
myCount = 0
Do While rng.Find.Found = True
' If you want to count them...
' Note where the end of the found item is
  endNow = rng.End
  rng.Start = rng.End
  rng.End = rng.End + 1
  
  If rng.Text = " " Then
    rng.Start = rng.End
    rng.End = rng.End + 1
  End If
  
  makeRoman = (rng.Font.Italic = False)
  rng.Start = endNow - 1
  rng.End = endNow
  
  If makeRoman Then
    rng.Font.Italic = False
    rng.HighlightColorIndex = changeToRomanColour
    myCount = myCount + 1
  Else
    If stayItalicColour > 0 Then rng.HighlightColorIndex = stayItalicColour
  End If
  
  ' Be sure you're past the previous occurrence
  rng.Start = endNow
  rng.End = endNow

' Go and find the next occurrence (if there is one)
  rng.Find.Execute
Loop
MsgBox "Changed italic to roman: " & myCount
ActiveDocument.TrackRevisions = myTrack
End Sub

