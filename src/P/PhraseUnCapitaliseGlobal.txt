
Sub PhraseUnCapitaliseGlobal()
' Paul Beverley - Version 21.07.25
' Lowercases two+ words in full caps

myColour = wdColorBlue
' myColour = wdColorBlack

doTrack = True
myTrack = ActiveDocument.TrackRevisions
If doTrack = False Then ActiveDocument.TrackRevisions = False

Beep
myResponse = MsgBox("Uncapitalise phrases in the WHOLE DOCUMENT?!", _
     vbQuestion + vbYesNo, "PhraseUnCapitaliseGlobal")
If myResponse <> vbYes Then Exit Sub

Set rng = ActiveDocument.Content
With rng.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "[A-Z\-]{2,} [A-Z ]{2,}"
  .Wrap = wdFindStop
  .Replacement.Text = ""
  .Forward = True
  .MatchWildcards = True
  .MatchWholeWord = False
  .Execute
End With

myCount = 0
Do While rng.Find.Found = True
  myCount = myCount + 1
  Set rngNext = rng.Duplicate
  rngNext.Collapse wdCollapseEnd
  rngNext.MoveStart , 1
  rng.Text = LCase(rng.Text)
  ActiveDocument.TrackRevisions = False
  If myColour > 0 Then rng.Font.color = myColour
  If doTrack = True Then ActiveDocument.TrackRevisions = myTrack
  rng.start = rngNext.start
  rng.Collapse wdCollapseStart
  rng.Find.Execute
  DoEvents
Loop
ActiveDocument.TrackRevisions = myTrack
Beep
MsgBox "Changed: " & myCount
End Sub


