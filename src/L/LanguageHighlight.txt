
Sub LanguageHighlight()
' Paul Beverley - Version 11.02.25
' Highlights all text NOT in this language

' Some colours you might use:
myColour = wdGray50
' myColour = wdGray25
' myColour = wdyellow

Set rng = Selection.Range.Duplicate
rng.Collapse wdCollapseStart
rng.MoveEnd , 1
myLanguage = rng.LanguageID
langName = Languages(myLanguage).NameLocal
CR = vbCr
CR2 = CR & CR
myResponse = MsgBox("Highlight all text NOT in this language?" & CR2 & _
     "          " & langName, vbQuestion + vbYesNo, "LanguageHighlight")
If myResponse <> vbYes Then Exit Sub

For Each myPara In ActiveDocument.Paragraphs
  If myPara.Range.LanguageID <> myLanguage Then
    If myPara.Range.LanguageID = 9999999 Then
      For Each myWord In myPara.Range.Words
        If myWord.LanguageID <> myLanguage Then
          myWord.HighlightColorIndex = myColour
        End If
      Next myWord
    Else
      myPara.Range.HighlightColorIndex = myColour
    End If
  End If
Next myPara
End Sub

