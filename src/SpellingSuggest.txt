Sub SpellingSuggest()
' Paul Beverley - Version 19.06.21
' Checks/corrects spellings or adds/subtracts FRedit suggested change

If LCase(Selection) = UCase(Selection) Then Selection.MoveLeft , 1
Selection.Expand wdWord
Do While InStr(ChrW(8217) & "' ", Right(Selection.Text, 1)) > 0
  Selection.MoveEnd , -1
  DoEvents
Loop
myWord = Selection
newWord = myWord

' Check spelling language
Set rng = ActiveDocument.Content
' (only first character, in case of split language)
rng.End = rng.Start + 1
langName = Languages(rng.LanguageID).NameLocal

Set rng = Selection.Range.Duplicate
rng.Expand wdParagraph
wdsInPara = rng.Words.Count
myPara = rng
initChar = Left(myWord, 1)
lastChar = Right(myWord, 1)

spellOK = Application.CheckSpelling(myWord, MainDictionary:=langName)
Set suggList = Application.GetSpellingSuggestions(myWord, _
     MainDictionary:=langName)

If suggList.Count > 0 And Not (spellOK) Then _
     newWord = suggList.Item(1).Name

' If initial letter is a capital keep the capital
If LCase(initChar) <> initChar Then _
     newWord = UCase(Left(newWord, 1)) & Mid(newWord, 2)
  
' If final letter is a capital, make it all caps
If LCase(lastChar) <> lastChar Then _
     newWord = UCase(newWord)

' If it's the only word in the para (a FRedit list)
If wdsInPara < 3 Then
  oldWord = Selection.Text
  Selection.Collapse wdCollapseEnd
  If Asc(Selection) = 32 Then
    Selection.MoveEnd , 1
    oldWord = oldWord & "^32"
    newWord = newWord & "^32"
    Else
    oldWord = oldWord & ">"
  End If
  Selection.Expand wdParagraph
  Selection.TypeText "~<" & oldWord & "|" & newWord & CR
  Selection.MoveRight , 1
  If newWord = myWord Then Beep
Else
  If newWord <> myWord Then
    Beep
    myTime = Timer
    Do
    Loop Until Timer > myTime + 0.2
    Beep
    Selection.TypeText newWord
  Else
    spellOK = Application.CheckSpelling(myWord, _
         MainDictionary:=langName)
    If spellOK Then
      Selection.Collapse wdCollapseEnd
      Beep
    Else
      If newWord = myWord Then
        Beep
        myTime = Timer
        Do
        Loop Until Timer > myTime + 0.2
        Beep
        If langName = "English (United States)" _
             And Application.CheckSpelling(myWord, _
             MainDictionary:="English (United Kingdom)") Then
          MsgBox ("Word's spellcheck is playing sillies!")
          Exit Sub
        Else
          MsgBox ("Word offers no suggestion")
        End If
      End If
    End If
  End If
End If
End Sub

