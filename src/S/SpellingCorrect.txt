Sub SpellingCorrect()
' Paul Beverley - Version 13.04.24
' Corrects spelling and wrong capitalisation

elistNameContains = "elist"

Selection.Collapse wdCollapseEnd
wasStart = Selection.Start
Set rng = Selection.Range.Duplicate
rng.MoveStart , -1
While UCase(rng) = LCase(rng)
  rng.MoveStart , -1
  rng.MoveEnd , -1
  DoEvents
Wend
rng.Expand wdWord
Do While InStr(ChrW(8217) & "' ", Right(rng.Text, 1)) > 0
  rng.MoveEnd , -1
  DoEvents
Loop
' Avoid single-character words
If Len(rng) < 2 Then
  rng.Select
  Beep
  MsgBox "Spellcheck this?!"
  Exit Sub
End If
' Check capitalisation, e.g. BEverley
initChar = rng.Characters(1)
scndChar = rng.Characters(2)
lastChar = rng.Characters.Last
doBeep = True
If UCase(initChar) = initChar And UCase(scndChar) = scndChar _
     And UCase(lastChar) <> lastChar Then
  doBeep = False
  rng.Characters(2) = LCase(scndChar)
End If
myWord = rng
newWord = myWord

' Check spelling language
' (check only first character, in case of split language)
langName = Languages(rng.Characters(1).LanguageID).NameLocal

' Check spelling
spellOK = Application.CheckSpelling(myWord, MainDictionary:=langName)

If Not (spellOK) Then
  For Each myDoc In Documents
    If InStr(LCase(myDoc.Name), elistNameContains) > 0 Then
      If InStr(myDoc.Content, vbCr & "myWord" & vbCr) > 0 Then
        spellOK = True
      End If
    End If
    DoEvents
  Next myDoc
End If
If Not (spellOK) Then
  Set suggList = Application.GetSpellingSuggestions(myWord, _
       MainDictionary:=langName)
  If suggList.Count > 0 Then newWord = suggList.Item(1).Name
  If newWord <> myWord Then
    rng.Text = newWord
  Else
    spellOK = Application.CheckSpelling(myWord, _
         MainDictionary:=langName)
    If spellOK = False Then
      If newWord = myWord Then
        Beep
        myTime = Timer
        Do
        Loop Until Timer > myTime + 1
        Beep
        If langName = "English (United States)" _
             And Application.CheckSpelling(myWord, _
             MainDictionary:="English (United Kingdom)") Then
          MsgBox ("Word's spellchecker is playing sillies!")
          Exit Sub
        Else
          MsgBox ("Word offers no suggestion")
        End If
      End If
    End If
  End If
Else
  If doBeep Then Beep
End If
rng.Start = wasStart - 2
rng.Expand wdWord
Do While InStr(ChrW(8217) & "' ", Right(rng.Text, 1)) > 0
  rng.MoveEnd , -1
  DoEvents
Loop
rng.Collapse wdCollapseEnd
rng.Select
End Sub

