Sub CitationAlyse()
' Paul Beverley - Version 22.12.21
' Checks citations against references

myFontColour = wdColorBlue
myDateColour = wdColorRed

includeNotes = True

CR = vbCr
CR2 = CR & CR

' If text is selected, sort it
If Selection.Start <> Selection.End Then
  myResponse = MsgBox("Sort selection?", vbQuestion _
       + vbOK, "CitationAlyse")
  If myResponse <> vbOK Then Exit Sub
  Dim myPara As Paragraph
  Set rng = Selection.Range.Duplicate
  For Each myPara In rng.Paragraphs
    myText = myPara.Range.Text
    For i = 2 To Len(myText) - 3
      myYear = Val(Trim(Mid(myText, i, 4)))
      If myYear > 1000 Then
        myPara.Range.InsertBefore Text:="]" & Trim(Str(myYear)) & "["
        Exit For
      End If
      DoEvents
    Next i
    DoEvents
  Next myPara
  Selection.Sort
  With Selection.Find
    .ClearFormatting
    .Replacement.ClearFormatting
    .Text = "]^#^#^#^#["
    .Wrap = wdFindStop
    .Forward = True
    .Replacement.Text = ""
    .MatchWildcards = False
    .Execute Replace:=wdReplaceAll
    DoEvents
  End With
  Exit Sub
End If

' Main citation listing section
Beep
myResponse = MsgBox("Is the cursor placed in the first item" _
     & CR2 & "of the references list?", vbQuestion _
     + vbYesNoCancel, "CitationAlyse")
If myResponse <> vbYes Then Exit Sub
On Error GoTo ReportIt
Application.ScreenUpdating = False

' copy references right to the end
Selection.Expand wdParagraph
Selection.Collapse wdCollapseStart
startRefs = Selection.Start
Set rngOld = ActiveDocument.Content
rngOld.Start = startRefs
rngOld.Copy
rngOld.Collapse wdCollapseStart
rngOld.Start = 0

' create new file of the text (not refs) + notes
Set mainDoc = ActiveDocument
Documents.Add
Selection.Text = rngOld.Text

' Copy footnotes and endnotes, text only
gotFoots = (mainDoc.Footnotes.count > 0)
gotEnds = (mainDoc.Endnotes.count > 0)
DoEvents

If gotFoots = True And includeNotes = True Then
  Selection.EndKey Unit:=wdStory
  Selection.TypeText Text:=vbCr
  Selection.Text = mainDoc.StoryRanges(wdFootnotesStory).Text
End If
If gotEnds = True And includeNotes = True Then
  Selection.EndKey Unit:=wdStory
  Selection.TypeText Text:=vbCr
  Selection.Text = mainDoc.StoryRanges(wdEndnotesStory).Text
End If
DoEvents


' Find and replace to create easy-to-search text
myList = "#ü|uu#û|qcq#~<([A-Z]{2}).|\1# in press| 2999#/| #" & _
     "~<([A-Z]).|\1#(| #)| #:| #;| #,| #" & _
     "~<([A-Z])> |\1ü#~<([A-Z]{2})> |\1ü#" & _
     "#.|ü#~, ([!0-9])|ü\1#(|ü#)|ü#[|ü#]|ü#" & _
     " et alii|üetüalüü# et al|üetüal# & |üandü#-|û#" & _
     "~<de la |DeüLaü#~<De la |DeüLaü#~<De La |DeüLaü#" & _
     "~([a-zA-Z]) e |\1üeü#~([a-zA-Z]) y |\1üyü#~<van |Vanü#~<de |Deü#" & _
     "~([a-zA-Z]) and |\1üandü#~<von |Vonü#~<van |Vanü#~<de |Deü#" & _
     "~<El |Elü#~<del |Delü#~<Del |Delü#~<la |Laü#~<La |Laü#" & _
     "~<le |Leü#~<di |Diü#~<dos |Dosü#~<la |Laü#~<le |Leü#" & _
     "~<der |Derü#üvon |üVonü#üvan |üVanü#üde |üDeü#üder |üDerü#" & _
     "üdi |üDiü#üdos |üDosü#üla |üLaü#üle |üLeü# ü| #ü | #" & _
     "~([0-9])ü|\1 #~ü([0-9])| \1#  | #"

FandR = Split(myList, "#")
Set rng = ActiveDocument.Content
For i = 1 To UBound(FandR)
  myFR = FandR(i)
  If Left(myFR, 1) <> ChrW(124) And _
       InStr(myFR, ChrW(124)) > 0 Then
    If Left(myFR, 1) = "~" Then
      myWild = True
      myFR = Mid(myFR, 2)
    Else
      myWild = False
    End If
    barpos = InStr(myFR, ChrW(124))
    myFind = Left(myFR, barpos - 1)
    myRep = Mid(myFR, barpos + 1)
    With rng.Find
      .ClearFormatting
      .Replacement.ClearFormatting
      .Text = myFind
      .Wrap = wdFindContinue
      .Forward = True
      .Replacement.Text = myRep
      .Replacement.Highlight = True
      .MatchWildcards = myWild
      .Execute Replace:=wdReplaceAll
    End With
    DoEvents
  End If
Next i

Set rng = ActiveDocument.Content
With rng.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "<[12][0-9]{3}[!0-9]"
  .Wrap = wdFindStop
  .Replacement.Text = ""
  .Forward = True
  .MatchWildcards = True
  .Execute
End With

' Find & Do to find all years
Do While rng.Find.Found = True
  myEnd = rng.End
  myYear = Replace(rng, "ü", "")
  myYear = Replace(myYear, ",", "")
  myYear = Replace(myYear, ";", "")
  myYear = Trim(myYear)
  rng.Collapse wdCollapseStart
  rng.MoveStart wdWord, -1
  myInit = Left(rng, 1)
  If UCase(myInit) <> myInit Or UCase(myInit) _
       = LCase(myInit) Then GoTo getNext
  wd1 = Trim(rng)
  rng.Collapse wdCollapseStart
  rng.MoveStart wdWord, -1
  wd2 = Trim(rng)
  myInit = Left(wd2, 1)
  gotTwo = (UCase(myInit) = myInit) And _
       (UCase(myInit) <> LCase(myInit))
' Find surname one and add to citeList1
  citeList1 = citeList1 & wd1 & " " & myYear & CR
' Find surname two and add to citeList2
  If gotTwo Then citeList2 = citeList2 & wd2 & _
       " " & wd1 & " " & myYear & CR
' Check if another date follows
  rng.Start = myEnd
  rng.End = myEnd
  Do
    rng.MoveEnd wdWord, 1
    If Len(rng) < 4 Then
      rng.Collapse wdCollapseEnd
      rng.MoveEnd wdWord, 1
    End If
    myYearNumber = Val(rng)
    If Val(rng) > 1000 Then
      myYear = Trim(rng)
      citeList1 = citeList1 & wd1 & " " & myYear & CR
      If gotTwo Then citeList2 = citeList2 & wd2 & _
           " " & wd1 & " " & myYear & CR
    End If
    rng.MoveStart wdWord, 1
    myEnd = rng.Start
    DoEvents
  Loop Until myYearNumber < 1000
getNext:
  rng.Start = myEnd
  rng.End = myEnd
  rng.Find.Execute
Loop

Set rng = ActiveDocument.Content
rng.Delete
Selection.TypeText Text:=citeList1
myEnd = ActiveDocument.Content.End
Selection.TypeText Text:=citeList2
Set rng = ActiveDocument.Content
rng.Start = myEnd
' rng.Font.Italic = True
Set rng = ActiveDocument.Content
rng.Font.Color = myFontColour

' Change by F&R û to -, ü to space, uu to ü, qcq to û
myList = "#ü| #uu|ü#û|-#qcq|û# 2999| in press#"

FandR = Split(myList, "#")
Set rng = ActiveDocument.Content
For i = 1 To UBound(FandR)
  myFR = FandR(i)
  If Left(myFR, 1) <> ChrW(124) And _
       InStr(myFR, ChrW(124)) > 0 Then
    barpos = InStr(myFR, ChrW(124))
    myFind = Left(myFR, barpos - 1)
    myRep = Mid(myFR, barpos + 1)
    With rng.Find
      .ClearFormatting
      .Replacement.ClearFormatting
      .Text = myFind
      .Wrap = wdFindContinue
      .Forward = True
      .Replacement.Text = myRep
      .MatchWildcards = False
      .Execute Replace:=wdReplaceAll
    End With
    DoEvents
  End If
Next i

Selection.WholeStory
Selection.Sort SortOrder:=wdSortOrderAscending, _
     SortFieldType:=wdSortFieldAlphanumeric

' Remove duplicates
For j = ActiveDocument.Paragraphs.count To 2 Step -1
  Set rng1 = ActiveDocument.Paragraphs(j).Range
  Set rng2 = ActiveDocument.Paragraphs(j - 1).Range
  If rng1 = rng2 Then rng1.Delete
  DoEvents
Next j

Selection.EndKey Unit:=wdStory
Selection.TypeText Text:=vbCr
wasEnd = Selection.End
Selection.Paste
Set rng = ActiveDocument.Content
rng.Start = wasEnd
rng.Fields.Unlink

With rng.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "^p "
  .Wrap = wdFindStop
  .Forward = True
  .Replacement.Text = "^p"
  .MatchWildcards = False
  .Execute Replace:=wdReplaceAll
  DoEvents
End With

' Add author name to "ditto" type references
myPrevAuthor = ""
For i = 1 To rng.Paragraphs.count
  Set myRef = rng.Paragraphs(i).Range
  myWord = Trim(myRef.Words(1))
  
  If Len(myRef.Text) > 1 And Len(myWord) = 1 Then
    myRef.Select
    myWord = myRef.Words(2)
  End If
  mySample = Left(myRef.Text, 2)
  If Len(myRef.Text) > 1 And LCase(mySample) = _
       UCase(mySample) Then
    myRef.Words(1) = myPrevAuthor & " "
  Else
    myPrevAuthor = myWord
  End If
  DoEvents
Next i

Set rng = ActiveDocument.Content
rng.Sort
DoEvents

Set rng = ActiveDocument.Content
rng.InsertAfter Text:=CR2
Set rng = ActiveDocument.Content
rng.ParagraphFormat.LineSpacingRule = wdLineSpaceSingle
rng.ParagraphFormat.SpaceBefore = 12
With rng.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "<[12][0-9a-j]{3,4}>"
  .Wrap = wdFindContinue
  .Forward = True
  .Replacement.Text = "^&"
  .Replacement.Font.Color = myDateColour
  .MatchWildcards = True
  .Execute Replace:=wdReplaceAll
  DoEvents
End With
Selection.HomeKey Unit:=wdStory
Selection.TypeText Text:="Citation Query List"
ActiveDocument.Paragraphs(1).Range.Style = wdStyleHeading2
' Remove blank lines at the top
Set para2 = ActiveDocument.Paragraphs(2)
Do While Len(para2.Range.Text) < 3
  para2.Range.Delete
Loop
Beep
Application.ScreenUpdating = True
Exit Sub

ReportIt:
Application.ScreenUpdating = True
On Error GoTo 0
Resume
End Sub

