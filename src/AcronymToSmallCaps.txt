Sub AcronymToSmallCaps()
' Paul Beverley - Version 31.12.13
' Convert all acronyms to small caps

myColour = False
' or if you want them highlighted:
' myColour = wdTurquoise

Selection.HomeKey Unit:=wdStory
With Selection.Find
  .ClearFormatting
  .Replacement.ClearFormatting
  .Text = "[A-Z]{3,}"
  .Wrap = False
  .Replacement.Text = ""
  .Forward = True
  .MatchWildcards = True
  .MatchWholeWord = False
  .MatchSoundsLike = False
  .Execute
End With

myCount = 0
Do While Selection.Find.Found = True
  Selection.Text = LCase(Selection.Text)
  If myColour <> False then Selection.Range.HighlightColorIndex = myColour
  Selection.Font.SmallCaps = True
  Selection.Collapse wdCollapseEnd
  Selection.Find.Execute
Loop
End Sub

