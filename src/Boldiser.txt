Sub Boldiser()
' Paul Beverley - Version 05.01.22
' Toggles next character or selected text bold.

If Selection.End = Selection.Start Then
  Selection.MoveEnd , 1
  Selection.Font.Bold = Not (Selection.Font.Bold = True)
  Selection.Collapse wdCollapseEnd
Else
' How many bold/roman chars?
  Set rng = Selection.Range.Duplicate
  For i = 1 To Len(rng.Text)
    isbold = rng.Characters(i).Font.Bold
    If isbold Then charsBold = charsBold + 1
  Next i
  charsRoman = Len(Selection) - charsBold
' Change to bold or roman, accordingly
  If charsBold > charsRoman Then
    Selection.Font.Bold = False
  Else
  ' Don't boldise following space or quote
    Do While InStr(ChrW(8217) & "' ", Right(rng.Text, 1)) > 0
      rng.MoveEnd , -1
      DoEvents
    Loop
    rng.Font.Bold = True
    rng.Select
  End If
End If
End Sub

